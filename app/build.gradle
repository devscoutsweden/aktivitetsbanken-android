apply plugin: 'com.android.application'

def getVersionCode = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'tag', '--list'
            standardOutput = stdout
        }
        return stdout.toString().split("\n").size()
    }
    catch (ignored) {
        return -1;
    }
}

def getVersionName = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags', '--long'
            standardOutput = stdout
        }
        def raw = stdout.toString().trim()
        def (prefix, versionName, versionOffsetCount, hash) = raw.tokenize('-')
        return versionName + (versionOffsetCount != '0' ? '.' + versionOffsetCount : '')
    }
    catch (ignored) {
        return null;
    }
}

android {
    defaultConfig {
        applicationId "se.devscout.android"

        versionCode getVersionCode()
        versionName getVersionName()

        minSdkVersion 15
        targetSdkVersion 15
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    compileSdkVersion 15
    buildToolsVersion "19.1.0"


    productFlavors {
        production {
            buildConfigField 'String', 'SERVER_API_HOST', '"aktivitetsbanken.devscout.se"'
            buildConfigField 'int', 'SERVER_API_PORT', '80'
        }
        staging {
            buildConfigField 'String', 'SERVER_API_HOST', '"aktivitetsbanken-test.devscout.se"'
            buildConfigField 'int', 'SERVER_API_PORT', '80'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile 'com.android.support:support-v4:19.1.0'
    compile 'com.google.android.gms:play-services:6.5.+'
    compile fileTree(dir: 'libs', include: ['*.jar'])
}
