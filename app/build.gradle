apply plugin: 'com.android.application'

def getVersionCode = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'tag', '--list'
            standardOutput = stdout
        }
        return stdout.toString().split("\n").size()
    }
    catch (ignored) {
        return -1;
    }
}

def getVersionName = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags', '--long'
            standardOutput = stdout
        }
        def raw = stdout.toString().trim()
        def (prefix, versionName, versionOffsetCount, hash) = raw.tokenize('-')
        return versionName + (versionOffsetCount != '0' ? '.' + versionOffsetCount : '')
    }
    catch (ignored) {
        return null;
    }
}

android {
    signingConfigs {
        devScoutDefault {
            keyAlias 'aktivitetsbanken-android'
            keyPassword AKTIVITETSBANKEN_APP_KEY_PASSWORD
            storeFile file('keys.keystore')
            storePassword AKTIVITETSBANKEN_APP_STORE_PASSWORD
        }
    }
    defaultConfig {
        applicationId "se.devscout.android"

        versionCode getVersionCode()
        versionName getVersionName()

        minSdkVersion 15
        targetSdkVersion 15
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    compileSdkVersion 15
    buildToolsVersion '23.0.2'
    dexOptions {
        dexInProcess = true
    }
    productFlavors {
        production {
            buildConfigField 'String', 'SERVER_API_HOST', '"aktivitetsbanken.devscout.se"'
            buildConfigField 'int', 'SERVER_API_PORT', '80'
        }
        staging {
            buildConfigField 'String', 'SERVER_API_HOST', '"scoutapi-mikaelsvensson.rhcloud.com"'
            buildConfigField 'int', 'SERVER_API_PORT', '80'
//            buildConfigField 'String', 'SERVER_API_HOST', '"82.183.20.4"'
//            buildConfigField 'int', 'SERVER_API_PORT', '8080'
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.devScoutDefault
        }
        debug {
            versionNameSuffix '-debug'
        }
    }
}

dependencies {
    compile 'com.android.support:support-v4:19.1.0'
    compile 'com.google.android.gms:play-services:6.5.+'
    compile fileTree(include: ['*.jar'], dir: 'libs')
}
